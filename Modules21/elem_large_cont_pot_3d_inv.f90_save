module elem_large_cont_pot_3d_inv

! Last rev.:
!  M. Ristinmaa 2011-10-07
!   - initial version 
!  M. Ristinmaa 2011-10-13
!   - modified to use global parameters
!------------------------------------------------------------------------------

!
!   Module elem_large_cont_pot_3d_inv contains element subroutines 
!   potential field to large deformations coupling
!   for the inverse motion problem

! 8 node brick element, trilinear displacement interpolation
! 2x2x2 gauss integration points (in total 8), the weight for every
! gauus point is one for this scheme


use matrix_util, only: inv3, det3

implicit none
! This is an attempt to predefine some vectors, to obtain speed
double precision  xsi(8), eta(8), zet(8), G1, Ifm(9)
integer           index(3,8)
parameter        (G1=0.577350269189626D0)
parameter        (xsi=(/-1D0,  1D0, 1D0, -1D0, -1D0,  1D0, 1D0, -1D0/)*G1 )
parameter        (eta=(/-1D0, -1D0, 1D0,  1D0, -1D0, -1D0, 1D0,  1D0/)*G1 )
parameter        (zet=(/ 1D0,  1D0, 1D0,  1D0, -1D0, -1D0,-1D0, -1D0/)*G1 )
parameter        (Ifm=(/1d0,0d0,0d0,0d0,1d0,0d0,0d0,0d0,1d0/))
parameter        (index=[(/1,2,3/),(/4,5,6/),(/7,8,9/),(/10,11,12/), &
                         (/13,14,15/),(/16,17,18/),(/19,20,21/),(/22,23,24/)])
private xsi, eta, zet, G1, Ifm, index

double precision  DNR(24,8) ! Maybe one should use DNR(8,24) instead for speed
! derivate of shape functions with respect to xsi
data             (DNR(1:24:3,1)= (1D0+ETA)*(1D0-ZET)/8D0)
data             (DNR(1:24:3,2)=-(1D0+ETA)*(1D0-ZET)/8D0)
data             (DNR(1:24:3,3)=-(1D0-ETA)*(1D0-ZET)/8D0)
data             (DNR(1:24:3,4)= (1D0-ETA)*(1D0-ZET)/8D0)
data             (DNR(1:24:3,5)= (1D0+ETA)*(1D0+ZET)/8D0)
data             (DNR(1:24:3,6)=-(1D0+ETA)*(1D0+ZET)/8D0)
data             (DNR(1:24:3,7)=-(1D0-ETA)*(1D0+ZET)/8D0)
data             (DNR(1:24:3,8)= (1D0-ETA)*(1D0+ZET)/8D0)
! derivate of shape functions with respect to eta
data             (DNR(2:24:3,1)= (1D0+XSI)*(1D0-ZET)/8D0)
data             (DNR(2:24:3,2)= (1D0-XSI)*(1D0-ZET)/8D0)
data             (DNR(2:24:3,3)=-(1D0-XSI)*(1D0-ZET)/8D0)
data             (DNR(2:24:3,4)=-(1D0+XSI)*(1D0-ZET)/8D0)
data             (DNR(2:24:3,5)= (1D0+XSI)*(1D0+ZET)/8D0)
data             (DNR(2:24:3,6)= (1D0-XSI)*(1D0+ZET)/8D0)
data             (DNR(2:24:3,7)=-(1D0-XSI)*(1D0+ZET)/8D0)
data             (DNR(2:24:3,8)=-(1D0+XSI)*(1D0+ZET)/8D0)
! derivate of shape functions with respect to zet
data             (DNR(3:24:3,1)=-(1D0+XSI)*(1D0+ETA)/8D0)
data             (DNR(3:24:3,2)=-(1D0-XSI)*(1D0+ETA)/8D0)
data             (DNR(3:24:3,3)=-(1D0-XSI)*(1D0-ETA)/8D0)
data             (DNR(3:24:3,4)=-(1D0+XSI)*(1D0-ETA)/8D0)
data             (DNR(3:24:3,5)= (1D0+XSI)*(1D0+ETA)/8D0)
data             (DNR(3:24:3,6)= (1D0-XSI)*(1D0+ETA)/8D0)
data             (DNR(3:24:3,7)= (1D0-XSI)*(1D0-ETA)/8D0)
data             (DNR(3:24:3,8)= (1D0+XSI)*(1D0-ETA)/8D0) 
private DNR

! local degrees of freedom
integer, parameter,dimension(24):: u_list=(/ 1, 2, 3, & 
                                               5, 6, 7, &
                                               9,10,11, &
                                              13,14,15, &
                                              17,18,19, &
                                              21,22,23, &
                                              25,26,27, &
                                              29,30,31/)
integer, parameter,dimension(8) :: p_list=(/4,8,12,16,20,24,28,32/)
private u_list, p_list

private det3, inv3
	
!------------------------------------------------------------------------------
contains


! 8-node brick element for use in a inverse motion formulation	
subroutine cp3dinv8_e(ke,coord,duu,dup,dpu,dpp,ed)
  implicit none
  double precision                :: ke(:,:), coord(:,:)
  double precision                :: duu(:,:,:), dup(:,:,:)
  double precision                :: dpu(:,:,:), dpp(:,:,:)
  double precision                :: ed(:)

  integer, parameter              :: NGP=8
  integer                         :: gp_nr

  double precision                :: JT(24,3), DetJ, JTinv(3,3)
  double precision                :: Bp(3,8)
  double precision                :: Bu(6,24)
  DOUBLE PRECISION                :: STRESS(3,3)
  DOUBLE PRECISION                :: HE(9,24)
  DOUBLE PRECISION                :: A(6,9), AH(6,24), dg(9)
  double precision                :: kuu(24,24), Kup(24,8), Kpu(8,24), Kpp(8,8)

  JT=MATMUL(DNR,transpose(coord))

  Kuu=0D0
  Kup=0D0
  Kpu=0D0
  Kpp=0D0
  do GP_NR=1,NGP

    call INV3(JTinv,JT(INDEX(:,gp_nr),:))
    Bp=MATMUL(JTinv,DNR(INDEX(:,gp_nr),:)) !dNx
    DETJ=det3(JT(index(:,gp_nr),:)) 

    Bu=0D0
    Bu(1,1:24:3)=Bp(1,:)
    Bu(2,2:24:3)=Bp(2,:)
    Bu(3,3:24:3)=Bp(3,:)
    Bu(4,1:24:3)=Bp(2,:)
    Bu(4,2:24:3)=Bp(1,:)
    Bu(5,1:24:3)=Bp(3,:)
    Bu(5,3:24:3)=Bp(1,:)
    Bu(6,2:24:3)=Bp(3,:)
    Bu(6,3:24:3)=Bp(2,:)
  
    He=0d0
    He(1:3,1:24:3)=Bp(1:3,:)
    He(4:6,2:24:3)=Bp(1:3,:)
    He(7:9,3:24:3)=Bp(1:3,:)

! deformation gradient
    dg=Ifm+matmul(He,ed(u_list))
    A=0d0
    A([1,4,5],1)=dg(1:3)
    A([2,4,6],2)=(/dg(2),dg(1),dg(3)/)
    A([3,5,6],3)=(/dg(3),dg(1),dg(2)/)
    A([1,4,5],4)=dg(4:6)
    A([2,4,6],5)=(/dg(5),dg(4),dg(6)/)
    A([3,5,6],6)=(/dg(6),dg(4),dg(5)/)
    A([1,4,5],7)=dg(7:9)
    A([2,4,6],8)=(/dg(8),dg(7),dg(9)/)
    A([3,5,6],9)=(/dg(9),dg(7),dg(8)/)
    A=2d0*A
    AH=matmul(A,He)

    kuu=kuu+(MATMUL(TRANSPOSE(Bu),MATMUL(Duu(:,:,gp_nr),AH)))*DETJ
    kup=kup-(MATMUL(TRANSPOSE(Bu),MATMUL(Dup(:,:,gp_nr),Bp)))*DETJ
    kpu=kpu-(MATMUL(TRANSPOSE(Bp),MATMUL(Dpu(:,:,gp_nr),AH)))*DETJ
    kpp=kpp+(MATMUL(TRANSPOSE(Bp),MATMUL(Dpp(:,:,gp_nr),Bp)))*DETJ

  end do
  
  Ke(u_list,u_list)=kuu
  Ke(u_list,p_list)=kup
  Ke(p_list,u_list)=kpu
  Ke(p_list,p_list)=kpp

  return   
end subroutine cp3dinv8_e


subroutine cp3dinv8_f(ef,coord,ed,es,d)
  implicit none

  double precision                :: ef(:), coord(:,:),  ed(:), es(:,:), d(:,:)

  integer, parameter              :: NGP=8
  integer                         :: gp_nr

  double precision                :: JT(24,3), JTinv(3,3)
  double precision                :: detJ
  double precision                :: Bu(6,24), Bp(3,8)
  double precision                :: fint_u(24)
  double precision                :: fint_p(8)

  JT=MATMUL(DNR,transpose(coord))

  fint_u=0D0
  fint_p=0D0
  do GP_NR=1,NGP
    call INV3(JTinv,JT(INDEX(:,gp_nr),:))
    Bp=MATMUL(JTinv,DNR(INDEX(:,gp_nr),:)) !dNx
    detJ=det3(JT(index(:,gp_nr),:)) 

    Bu=0D0
    Bu(1,1:24:3) =Bp(1,:)
    Bu(2,2:24:3) =Bp(2,:)
    Bu(3,3:24:3) =Bp(3,:)
    Bu(4,1:24:3) =Bp(2,:)
    Bu(4,2:24:3) =Bp(1,:)
    Bu(5,1:24:3) =Bp(3,:)
    Bu(5,3:24:3) =Bp(1,:)
    Bu(6,2:24:3) =Bp(3,:)
    Bu(6,3:24:3) =Bp(2,:)

    fint_u=fint_u+MATMUL(TRANSPOSE(Bu),es(:,gp_nr))*detJ
    fint_p=fint_p-MATMUL(TRANSPOSE(Bp),d(:,gp_nr))*detJ

  end do

  ef(u_list)=fint_u
  ef(p_list)=fint_p

  return
end subroutine cp3dinv8_f



! calculates the total inverse deformation gradient and the Electric field
subroutine cp3dinv8_d(dg,de,coord,ed)
  implicit none

  double precision                :: dg(:,:), de(:,:), coord(:,:),  ed(:)

  integer, parameter              :: NGP=8
  integer                         :: gp_nr

  double precision                :: JT(24,3), JTinv(3,3)
  double precision                :: DNX(3,8), He(9,24)
  
  double precision                :: invF(3,3)
  double precision                :: p_val(8)

  p_val=ed(p_list)

  JT=MATMUL(DNR,transpose(COORD))
   
  do GP_NR=1,NGP
    call INV3(JTinv,JT(INDEX(:,gp_nr),:))
    dNx=MATMUL(JTinv,DNR(INDEX(:,gp_nr),:)) 

! Rewrite such that dg is obtained directly
    He=0D0
    He(1:3,1:24:3)=dNx(1:3,:)
    He(4:6,2:24:3)=dNx(1:3,:)
    He(7:9,3:24:3)=dNx(1:3,:)
     
! Deformation gradient      
    dg(:,gp_nr)=Ifm+MATMUL(HE,ED(u_list))

! Spatial gradient of scalar field
    de(:,gp_nr)=-matmul(dNx,p_val)

  end do
  
  return
end subroutine cp3dinv8_d


end module elem_large_cont_pot_3d_inv
